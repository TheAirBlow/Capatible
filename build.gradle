plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group = 'net.theairblow'
version = '1.0.2'

java {
    archivesBaseName = 'capatible'
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                capatible {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                capatible {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url 'https://cdn.sussy.tech/repo2/'
    }
    maven {
        url "https://maven.cleanroommc.com"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    implementation 'eu.infomas:annotation-detector:3.1.0-SNAPSHOT'
    implementation fg.deobf("zone.rong:mixinbooter:7.0")
}

shadowJar {
    dependencies {
        exclude '*.dd' // For some reason it excluded everything except includes
        include dependency('eu.infomas:annotation-detector:3.1.0-SNAPSHOT')
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "capatible",
                "Specification-Vendor"    : "TheAirBlow",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "TheAirBlow",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.theairblow'
            artifactId = 'capatible'
            version = '1.0.2'

            from components.java
            pom {
                name = 'Capatible!'
                description = 'Makes so modding doesn\'t make my life miserable'
                url = 'https://github.com/TheAirBlow/Capatible'
                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://github.com/TheAirBlow/Capatible/blob/main/LICENCE'
                    }
                }
                developers {
                    developer {
                        id = 'theairblow'
                        name = 'TheAirBlow'
                        email = 'theairblow@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TheAirBlow/Capatible.git'
                    developerConnection = 'scm:git:ssh://github.com/TheAirBlow/Capatible.git'
                    url = 'https://github.com/TheAirBlow/Capatible'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('repo')
        }
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
